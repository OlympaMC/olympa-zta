plugins {
    id 'java-library'
}

repositories {
    mavenLocal()

    maven {
        url = uri('https://repo.citizensnpcs.co/')
        content{
       		includeGroup "net.citizensnpc"
        	includeGroup "org.mcmonkey"
        }
    }

    maven {
        url = uri('https://repo.mikeprimm.com/')
    }

    maven {
        url = uri('https://repo.codemc.org/repository/maven-public/')
    }
}

configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("fr.olympa:olympa-api") with project(':olympa-api')
    }
}

dependencies {
    compileOnly 'fr.olympa:olympa-api:0.0.1-SNAPSHOT'
    compileOnly 'net.citizensnpcs:citizens-main:2.0.27-SNAPSHOT'
    compileOnly 'net.citizensnpcs:citizens-v1_16_R3:2.0.27-SNAPSHOT'
    compileOnly 'org.mcmonkey:sentinel:2.2.6-SNAPSHOT'
    compileOnly 'fr.skytasul:beautyquests-core:0.19.0-SNAPSHOT'
    compileOnly('us.dynmap:dynmap-api:3.1-SNAPSHOT'){
    	exclude group: 'org.bukkit', module: 'bukkit'
    }
}

def getCheckedOutGitCommitHash() {
    def gitFolder = "$projectDir/.git/"
    def takeFromHash = 8
    /*
     * '.git/HEAD' contains either
     *      in case of detached head: the currently checked out commit hash
     *      otherwise: a reference to a file containing the current commit hash
     */
    def head = new File(gitFolder + "HEAD").text.split(":") // .git/HEAD
    def isCommit = head.length == 1 // e5a7c79edabbf7dd39888442df081b1c9d8e88fd
    // def isRef = head.length > 1     // ref: refs/heads/master

    if(isCommit) return head[0].trim().take(takeFromHash) // e5a7c79edabb

    def refHead = new File(gitFolder + head[1].trim()) // .git/refs/heads/master
    refHead.text.trim().take takeFromHash
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMddHHmmss')
    return formattedDate
}

group = 'fr.olympa'
version = '0.0.2-SNAPSHOT'
description = 'olympa-zta'
java.sourceCompatibility = JavaVersion.VERSION_16

jar {
	archiveFileName = 'OlympaZTA.jar'
	destinationDirectory = file('build')
}

processResources {
	outputs.upToDateWhen { false }
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
       "plugin_version": version + "-" + getGitBranch() + '-' + getCheckedOutGitCommitHash() + "-" + getDate()
    ]
}

sourceSets {
    main {
        java {
            srcDirs = ['src/java']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
